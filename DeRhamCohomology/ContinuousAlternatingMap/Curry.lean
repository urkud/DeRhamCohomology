/-
Copyright (c) 2024 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.NormedSpace.Alternating.Basic
import Mathlib.Analysis.NormedSpace.Multilinear.Curry
import Mathlib.LinearAlgebra.Alternating.DomCoprod
import Mathlib.LinearAlgebra.Alternating.Uncurry.Fin
import DeRhamCohomology.Alternating.Basic
import DeRhamCohomology.Multilinear.Basic

noncomputable section
suppress_compilation

namespace ContinuousAlternatingMap

section Curry

variable {ğ•œ E F Î¹ Î¹' : Type*} [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  [Fintype Î¹] [Fintype Î¹']
  {m n : â„•}

def uncurryFin (f : E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F) : E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F :=
  AlternatingMap.mkContinuous (.uncurryFin <| toAlternatingMapLinear âˆ˜â‚— f)
    ((n + 1) * â€–fâ€–) fun v â†¦ calc
      _ = â€–âˆ‘ k, (-1) ^ k.val â€¢ f (v k) (k.removeNth v)â€– := by
        simp [AlternatingMap.uncurryFin_apply]
      _ â‰¤ âˆ‘ k, â€–fâ€– * â€–v kâ€– * âˆ j, â€–v (k.succAbove j)â€– := by
        refine norm_sum_le_of_le _ fun k _ â†¦ ?_
        rw [norm_isUnit_zsmul _ (.pow _ isUnit_one.neg)]
        exact (f (v k)).le_of_opNorm_le (f.le_opNorm _) _
      _ = _ := by
        simp [mul_assoc, â† Fin.prod_univ_succAbove (â€–v Â·â€–)]

lemma toAlternatingMap_uncurryFin (f : E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F) :
    (uncurryFin f).toAlternatingMap = .uncurryFin (toAlternatingMapLinear âˆ˜â‚— f) :=
  rfl

theorem norm_uncurryFin_le (f : E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F) :
    â€–uncurryFin fâ€– â‰¤ (n + 1) * â€–fâ€– :=
  AlternatingMap.mkContinuous_norm_le _ (by positivity) _

theorem uncurryFin_apply (f : E â†’L[ğ•œ] (E [â‹€^Fin n]â†’L[ğ•œ] F)) (v : Fin (n + 1) â†’ E) :
    uncurryFin f v = âˆ‘ k, (-1) ^ k.val â€¢ f (v k) (k.removeNth v) :=
  AlternatingMap.uncurryFin_apply ..

theorem uncurryFin_add (f g : E â†’L[ğ•œ] (E [â‹€^Fin n]â†’L[ğ•œ] F)) :
    uncurryFin (f + g) = uncurryFin f + uncurryFin g := by
  ext v
  simp [uncurryFin_apply, Finset.sum_add_distrib]

theorem uncurryFin_smul {M : Type*} [Monoid M] [DistribMulAction M F] [ContinuousConstSMul M F]
    [SMulCommClass ğ•œ M F] (c : M) (f : E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F) :
    uncurryFin (c â€¢ f) = c â€¢ uncurryFin f := by
  ext v
  simp [uncurryFin_apply, smul_comm _ c, Finset.smul_sum]

@[simps! apply]
def uncurryFinCLM :
    (E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F) â†’L[ğ•œ] E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F :=
  LinearMap.mkContinuous
    { toFun := uncurryFin (ğ•œ := ğ•œ) (E := E) (F := F) (n := n)
      map_add' := by exact uncurryFin_add -- TODO: why does it fail without `by exact`?
      map_smul' := by exact uncurryFin_smul }
    (n + 1) norm_uncurryFin_le

theorem uncurryFin_uncurryFinCLM_comp_of_symmetric {f : E â†’L[ğ•œ] E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F}
    (hf : âˆ€ x y, f x y = f y x) :
    uncurryFin (uncurryFinCLM.comp f) = 0 :=
  toAlternatingMap_injective <| AlternatingMap.uncurryFin_uncurryFinLM_comp_of_symmetric
    (f := .comprâ‚‚ f.toLinearMapâ‚‚ toAlternatingMapLinear) fun x y â†¦ congr(toAlternatingMap $(hf x y))

/- Interior product -/
def curryFin (f : E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F) : E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F :=
  LinearMap.mkContinuous
    { toFun := fun x =>
        { toContinuousMultilinearMap := f.1.curryLeft x
          map_eq_zero_of_eq' := fun v i j hv hne â†¦ by
            apply f.map_eq_zero_of_eq (Fin.cons x v) (i := i.succ) (j := j.succ) <;> simpa }
      map_add' := fun x y => by ext; simp
      map_smul' := fun c x => by ext; simp }
    â€–fâ€– fun x => by
      rw [LinearMap.coe_mk, AddHom.coe_mk, â† norm_toContinuousMultilinearMap]
      dsimp
      refine ContinuousLinearMap.le_of_opNorm_le f.curryLeft ?_ x
      apply le_of_eq
      exact ContinuousMultilinearMap.curryLeft_norm f.toContinuousMultilinearMap

theorem curryFin_apply (f : E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F) (x : E) (m : Fin n â†’ E) :
    curryFin f x m = f (Fin.cons x m) :=
  rfl

theorem curryFin_add (f g : E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F) :
    curryFin (f + g) = curryFin f + curryFin g := by
  ext e v
  simp [curryFin_apply]

theorem curryFin_smul {M : Type*} [Monoid M] [DistribMulAction M F] [ContinuousConstSMul M F]
    [SMulCommClass ğ•œ M F] (c : M) (f : E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F) :
    curryFin (c â€¢ f) = c â€¢ curryFin f := by
  ext e v
  simp [curryFin_apply]

variable [DecidableEq Î¹] [DecidableEq Î¹']

/-- summand used in `ContinuousAlternatingMap.uncurrySum` -/
def uncurrySum.summand (f : E [â‹€^Î¹]â†’L[ğ•œ] E [â‹€^Î¹']â†’L[ğ•œ] F) (Ïƒ : Equiv.Perm.ModSumCongr Î¹ Î¹') :
    ContinuousMultilinearMap ğ•œ (fun _ : Î¹ âŠ• Î¹' => E) F :=
  Quotient.liftOn' Ïƒ
    (fun Ïƒ =>
      Equiv.Perm.sign Ïƒ â€¢
        (ContinuousMultilinearMap.uncurrySum
          (f.toContinuousMultilinearMap.flipAlternating.toContinuousMultilinearMap.flipMultilinear)
            : ContinuousMultilinearMap ğ•œ (fun _ => E) (F)).domDomCongr Ïƒ)
    fun Ïƒâ‚ Ïƒâ‚‚ H => by
      rw [QuotientGroup.leftRel_apply] at H
      obtain âŸ¨âŸ¨sl, srâŸ©, hâŸ© := H
      ext v
      simp [ContinuousMultilinearMap.domDomCongr_apply, ContinuousMultilinearMap.uncurrySum_apply,
            MultilinearMap.smul_apply]
      replace h := inv_mul_eq_iff_eq_mul.mp h.symm
      have : Equiv.Perm.sign (Ïƒâ‚ * Equiv.Perm.sumCongrHom _ _ (sl, sr))
        = Equiv.Perm.sign Ïƒâ‚ * (Equiv.Perm.sign sl * Equiv.Perm.sign sr) := by simp
      rw [h, this, mul_smul, mul_smul, smul_left_cancel_iff, smul_comm]
      simp [ContinuousMultilinearMap.flipMultilinear]
      erw [â† (f.flipAlternating ((fun i â†¦ v (Ïƒâ‚ (Sum.map (â‡‘sl) (â‡‘sr) i))) âˆ˜ Sum.inr)).map_congr_perm fun i => v (Ïƒâ‚ _)]
      simp [ContinuousAlternatingMap.flipAlternating]
      erw [â† (f fun i â†¦ v (Ïƒâ‚ (Sum.inl i))).map_congr_perm fun i => v (Ïƒâ‚ _)]
      simp [ContinuousMultilinearMap.flipAlternating]
      rfl

theorem uncurrySum.summand_mk (f : E [â‹€^Î¹]â†’L[ğ•œ] E [â‹€^Î¹']â†’L[ğ•œ] F) (Ïƒ : Equiv.Perm (Î¹ âŠ• Î¹')) :
    uncurrySum.summand f (Quot.mk
      (â‡‘(QuotientGroup.leftRel (Equiv.Perm.sumCongrHom Î¹ Î¹').range)) Ïƒ) = Equiv.Perm.sign Ïƒ â€¢
        (ContinuousMultilinearMap.uncurrySum
          (f.toContinuousMultilinearMap.flipAlternating.toContinuousMultilinearMap.flipMultilinear) :
            ContinuousMultilinearMap ğ•œ (fun _ => E) F).domDomCongr Ïƒ :=
  rfl

theorem uncurrySum.summand_mk'' (f : E [â‹€^Î¹]â†’L[ğ•œ] E [â‹€^Î¹']â†’L[ğ•œ] F) (Ïƒ : Equiv.Perm (Î¹ âŠ• Î¹')) :
    uncurrySum.summand f (Quotient.mk'' Ïƒ) = Equiv.Perm.sign Ïƒ â€¢
      (ContinuousMultilinearMap.uncurrySum
        (f.toContinuousMultilinearMap.flipAlternating.toContinuousMultilinearMap.flipMultilinear) :
          ContinuousMultilinearMap ğ•œ (fun _ => E) F).domDomCongr Ïƒ :=
  rfl

/-- Swapping elements in `Ïƒ` with equal values in `v` results in an addition that cancels -/
theorem uncurrySum.summand_add_swap_smul_eq_zero (f : E [â‹€^Î¹]â†’L[ğ•œ] E [â‹€^Î¹']â†’L[ğ•œ] F)
    (Ïƒ : Equiv.Perm.ModSumCongr Î¹ Î¹') {v : Î¹ âŠ• Î¹' â†’ E}
    {i j : Î¹ âŠ• Î¹'} (hv : v i = v j) (hij : i â‰  j) :
    uncurrySum.summand f Ïƒ v + uncurrySum.summand f (Equiv.swap i j â€¢ Ïƒ) v = 0 := by
  refine Quotient.inductionOn' Ïƒ fun Ïƒ => ?_
  dsimp only [Quotient.liftOn'_mk'', Quotient.map'_mk'', MulAction.Quotient.smul_mk,
    uncurrySum.summand]
  rw [smul_eq_mul, Equiv.Perm.sign_mul, Equiv.Perm.sign_swap hij]
  simp only [one_mul, neg_mul, Function.comp_apply, Units.neg_smul, Equiv.Perm.coe_mul, Units.val_neg,
    ContinuousMultilinearMap.smul_apply, ContinuousMultilinearMap.neg_apply, ContinuousMultilinearMap.domDomCongr_apply,
    ContinuousMultilinearMap.uncurrySum_apply]
  convert add_neg_cancel (G := F) _ using 6 <;>
    Â· ext k
      simp [Function.comp_apply, Function.comp_apply, Equiv.apply_swap_eq_self hv]

/-- Swapping elements in `Ïƒ` with equal values in `v` result in zero if the swap has no effect
on the quotient. -/
theorem uncurrySum.summand_eq_zero_of_smul_invariant (f : E [â‹€^Î¹]â†’L[ğ•œ] E [â‹€^Î¹']â†’L[ğ•œ] F)
    (Ïƒ : Equiv.Perm.ModSumCongr Î¹ Î¹') {v : Î¹ âŠ• Î¹' â†’ E}
    {i j : Î¹ âŠ• Î¹'} (hv : v i = v j) (hij : i â‰  j) :
    Equiv.swap i j â€¢ Ïƒ = Ïƒ â†’ uncurrySum.summand f Ïƒ v = 0 := by
  refine Quotient.inductionOn' Ïƒ fun Ïƒ => ?_
  dsimp only [Quotient.liftOn'_mk'', Quotient.map'_mk'', ContinuousMultilinearMap.smul_apply,
    ContinuousMultilinearMap.domDomCongr_apply, ContinuousMultilinearMap.uncurrySum_apply, uncurrySum.summand]
  intro hÏƒ
  cases' hi : Ïƒâ»Â¹ i with val val <;> cases' hj : Ïƒâ»Â¹ j with val_1 val_1 <;>
    rw [Equiv.Perm.inv_eq_iff_eq] at hi hj <;> substs hi hj <;> revert val val_1
  -- the term pairs with and cancels another term
  case inl.inr =>
    intro i' j' _ _ hÏƒ
    obtain âŸ¨âŸ¨sl, srâŸ©, hÏƒâŸ© := QuotientGroup.leftRel_apply.mp (Quotient.exact' hÏƒ)
    replace hÏƒ := Equiv.congr_fun hÏƒ (Sum.inl i')
    dsimp only at hÏƒ
    rw [smul_eq_mul, â† Equiv.mul_swap_eq_swap_mul, mul_inv_rev, Equiv.swap_inv, inv_mul_cancel_right] at hÏƒ
    simp at hÏƒ
  case inr.inl =>
    intro i' j' _ _ hÏƒ
    obtain âŸ¨âŸ¨sl, srâŸ©, hÏƒâŸ© := QuotientGroup.leftRel_apply.mp (Quotient.exact' hÏƒ)
    replace hÏƒ := Equiv.congr_fun hÏƒ (Sum.inr i')
    dsimp only at hÏƒ
    rw [smul_eq_mul, â† Equiv.mul_swap_eq_swap_mul, mul_inv_rev, Equiv.swap_inv, inv_mul_cancel_right] at hÏƒ
    simp at hÏƒ
  -- the term does not pair but is zero
  case inr.inr =>
    intro i' j' hv hij _
    convert smul_zero (M := â„¤Ë£) (A := F) _
    exact ContinuousAlternatingMap.map_eq_zero_of_eq _ _ hv fun hij' => hij (hij' â–¸ rfl)
  case inl.inl =>
    intro i' j' hv hij _
    convert smul_zero (M := â„¤Ë£) (A := F) _
    simp [ContinuousMultilinearMap.flipMultilinear]
    exact ContinuousAlternatingMap.map_eq_zero_of_eq ((f.flipAlternating ((fun i â†¦ v (Ïƒ i)) âˆ˜ Sum.inr))) _ hv
      fun hij' => hij (hij' â–¸ rfl)

def uncurrySum (f : E [â‹€^Î¹]â†’L[ğ•œ] E [â‹€^Î¹']â†’L[ğ•œ] F) : E [â‹€^Î¹ âŠ• Î¹']â†’L[ğ•œ] F :=
    { âˆ‘ Ïƒ : Equiv.Perm.ModSumCongr Î¹ Î¹', uncurrySum.summand f Ïƒ with
    toFun := fun v => (â‡‘(âˆ‘ Ïƒ : Equiv.Perm.ModSumCongr Î¹ Î¹', uncurrySum.summand f Ïƒ)) v
    map_eq_zero_of_eq' := fun v i j hv hij => by
      rw [ContinuousMultilinearMap.sum_apply]
      exact
        Finset.sum_involution (fun Ïƒ _ => Equiv.swap i j â€¢ Ïƒ)
          (fun Ïƒ _ => uncurrySum.summand_add_swap_smul_eq_zero f Ïƒ hv hij)
          (fun Ïƒ _ => mt <| uncurrySum.summand_eq_zero_of_smul_invariant f Ïƒ hv hij)
          (fun Ïƒ _ => Finset.mem_univ _) fun Ïƒ _ =>
          Equiv.swap_smul_involutive i j Ïƒ }

theorem uncurrySum_coe (f : E [â‹€^Î¹]â†’L[ğ•œ] E [â‹€^Î¹']â†’L[ğ•œ] F) :
    ((uncurrySum f).toContinuousMultilinearMap : ContinuousMultilinearMap ğ•œ (fun _ => E) F) =
      âˆ‘ Ïƒ : Equiv.Perm.ModSumCongr Î¹ Î¹', uncurrySum.summand f Ïƒ :=
  ContinuousMultilinearMap.ext fun _ => rfl

theorem uncurrySum_apply (f : E [â‹€^Î¹]â†’L[ğ•œ] E [â‹€^Î¹']â†’L[ğ•œ] F) (m : Î¹ âŠ• Î¹' â†’ E) :
    uncurrySum f m = (âˆ‘ Ïƒ : Equiv.Perm.ModSumCongr Î¹ Î¹', uncurrySum.summand f Ïƒ) m :=
  rfl

def uncurryFinAdd (f : E [â‹€^Fin m]â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F) :
    E [â‹€^Fin (m + n)]â†’L[ğ•œ] F :=
  ContinuousAlternatingMap.domDomCongr finSumFinEquiv (uncurrySum f)

end Curry

end ContinuousAlternatingMap
